define pred list^(x): 
  ( 
	((x l= nil) & emp) | 
          ((x |-> loc next: nxt; int key: ky) * list^(nxt))
  )
  axiom: (lseg^(x, x) * (((x l= nil) & (length^(x) i= 0)) | ((~ (x l= nil)) & (0 < length^(x)))));
  

define set-fun keys^(x):
  (case (x l= nil): emptyset;
   case ((x |-> loc next: nxt; int key: ky) * true): 
   	((singleton ky) union keys^(nxt));
   default: emptyset
  ) ;

define int-fun length^(x):
  (case (x l= nil): 0;
   case ((x |-> loc next: nxt; int key: ky) * true): 
   	(1 + length^(nxt));
   default: 0
  ) ;

define relation lseg^(head, tail): 
  ( 
	(((head l= tail) | ((head l= nil) & (tail l= nil))) & emp) | 
          ((head |-> loc next: nxt; int key: ky) * lseg^(nxt, tail))  
  ) 
axiom: ( 
			((((head l= tail) => (emp & ((lseg-length^(head, tail) i= 0) & (lseg-keys^(head, tail) s= emptyset)))) &
			((tail l= nil) => (list^(head) & ((length^(head) i= lseg-length^(head, tail)) & (keys^(head) s= lseg-keys^(head, tail)))))) &
			(list^(tail) -* (list^(head) & ((keys^(head) s= (lseg-keys^(head, tail) union keys^(tail))) & (length^(head) i= (lseg-length^(head, tail) + length^(tail))))))) &
			(((tail |-> loc next: virtual tn; int key: virtual tk) * list^(tn)) -* ((lseg^(head, tn) & ((lseg-keys^(head, tn) s= (lseg-keys^(head, tail) union (singleton tk))) & (lseg-length^(head, tn) i= (lseg-length^(head, tail) + 1))) ) * list^(tn)))
	   ) ;

define bin-set-fun lseg-keys^(head, tail):
  (case ((head l= tail) | ((head l= nil) & (tail l= nil))) : emptyset;
   case ((head |-> loc next: nxt; int key: ky) * true): 
   	((singleton ky) union lseg-keys^(nxt, tail));
   default: emptyset
  ) ;

define bin-int-fun lseg-length^(head, tail):
  (case (head l= tail) : 0;
   case ((head |-> loc next: nxt; int key: ky) * true): 
   	(1 + lseg-length^(nxt, tail));
   default: 0
  ) ;


bb find-before-loop:
pre: (list^(x) & (oldx l= x));
post: (list^(oldx) & ((lseg^(oldx, x) & (~ (d i-in lseg-keys^(oldx, x)))) * list^(x)));
{
	loc tmp := nil;
}

bb find-in-loop:
pre: (list^(oldx) & ((lseg^(oldx, x) & (~ (d i-in lseg-keys^(oldx, x)))) * list^(x)));
post: (list^(oldx) & ((lseg^(oldx, x) & (~ (d i-in lseg-keys^(oldx, x)))) * list^(x)));
{
	assume (! (x l== nil));
	int xk := x.key;
	assume (! (xk i== d));
	loc tmp := x.next;
	loc x := tmp;
}

bb find-after-loop1:
pre: (list^(oldx) & ((lseg^(oldx, x) & (~ (d i-in lseg-keys^(oldx, x)))) * list^(x)));
post: ((list^(oldx) & ((lseg^(oldx, ret) & (~ (d i-in lseg-keys^(oldx, ret)))) * list^(ret))) &
	(((d i-in keys^(oldx)) & ((ret |-> int key: d) * true)) | ((~ (d i-in keys^(oldx))) & (ret l= nil)))) ;
{
	assume (x l== nil);
	loc ret := x;
}

bb find-after-loop2:
pre: (list^(oldx) & ((lseg^(oldx, x) & (~ (d i-in lseg-keys^(oldx, x)))) * list^(x)));
post: ((list^(oldx) & ((lseg^(oldx, ret) & (~ (d i-in lseg-keys^(oldx, ret)))) * list^(ret))) &
	(((d i-in keys^(oldx)) & ((ret |-> int key: d) * true)) | ((~ (d i-in keys^(oldx))) & (ret l= nil)))) ;
{
	assume (! (x l== nil));
	int xk := x.key;
	assume (xk i== d);
	loc ret := x;
}