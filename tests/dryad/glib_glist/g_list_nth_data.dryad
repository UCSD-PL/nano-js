define pred dll^(x):
 (
	 (((x l= nil) & emp) |

	  ((x |-> loc next: y) & (y l= nil))) |

	   (
	        (((x |-> loc next: nxt) * (nxt |-> secondary prev: x)) * true) &
	        (  (x |-> loc next: nxt) * ((~(nxt l= nil)) & dll^(nxt)) )
		)
 )
 axiom: sll^(x);
	 
define set-fun keys^(x):
  (case (x l= nil): emptyset;
   case ((x |-> loc next: nxt; int key: ky) * true): 
   	((singleton ky) union keys^(nxt));
   default: emptyset
  ) ;

define int-fun length^(x):
  (case (x l= nil): 0;
   case ((x |-> loc next: nxt; int key: ky) * true): 
   	(1 + length^(nxt));
   default: 0
  ) ;

define pred sll^(x): 
  ( 
	((x l= nil) & emp) | 
          ((x |-> loc next: nxt) * sll^(nxt))  
  )
  axiom: (lseg^(x, x) * (((x l= nil) & (length^(x) i= 0)) | ((~ (x l= nil)) & (0 < length^(x)))));

define relation lseg^(head, tail): 
  ( 
	((head l= tail) & emp) | 
          ((head |-> loc next: nxt) * lseg^(nxt, tail))  
  ) 
  axiom: ( (((head l= tail) => (emp & ((lseg-length^(head, tail) i= 0) & (lseg-keys^(head, tail) s= emptyset)))) & ((tail l= nil) => (sll^(head) & ((length^(head) i= lseg-length^(head, tail)) & (keys^(head) s= lseg-keys^(head, tail)))))) &
	( (sll^(tail) -* (sll^(head) & ( (keys^(head) s= (lseg-keys^(head, tail) union keys^(tail))) & (length^(head) i= (lseg-length^(head, tail) + length^(tail))) ))) &
	  (((tail |-> loc next: virtual tn) * sll^(tn)) -* ((lseg^(head, tn) & ( (lseg-keys^(head, tn) s= (lseg-keys^(head, tail) union (singleton tk))) & (lseg-length^(head, tn) i= (lseg-length^(head, tail) + 1)) )) * sll^(tn))) ) ) ;

define bin-set-fun lseg-keys^(head, tail):
  (case (head l= tail) : emptyset;
   case ((head |-> loc next: nxt; int key: ky) * true): 
   	((singleton ky) union lseg-keys^(nxt, tail));
   default: emptyset
  ) ;

define bin-int-fun lseg-length^(head, tail):
  (case (head l= tail) : 0;
   case ((head |-> loc next: nxt; int key: ky) * true): 
   	(1 + lseg-length^(nxt, tail));
   default: 0
  ) ;



bb g-list-nth-data-before-loop:
pre: (dll^(list) & (0 <= n));
post: ((dll^(list) & ((lseg^(list, curr) & ((lseg-length^(list, curr) + order) i= n)) * dll^(curr))) & (0 <= order));
{
	loc curr := list;
	int order := n;
}

bb g-list-nth-data-in-loop:
pre: ((dll^(list) & ((lseg^(list, curr) & ((lseg-length^(list, curr) + order) i= n)) * dll^(curr))) & (0 <= order));
post: ((dll^(list) & ((lseg^(list, curr1) & ((lseg-length^(list, curr1) + order1) i= n)) * dll^(curr1))) & (0 <= order1));
{
	assume ((0 < order) && (! (curr l== nil)));
	int order1 := order - 1;
	loc curr1 := curr.next;
}

bb g-list-nth-data-after-loop-not-nil:
pre: ((dll^(list) & ((lseg^(list, curr) & ((lseg-length^(list, curr) + order) i= n)) * dll^(curr))) & (0 <= order));
post: (dll^(list) & (((length^(list) <= n) & (ret i= 0)) | ((n < length^(list)) & (((lseg^(list, curr) & (lseg-length^(list, curr) i= n)) * dll^(curr)) & ((curr l= nil) | ((curr |-> int key: ret) * true))))));
{
	assume ((! (0 < order)) || (curr l== nil));
	assume (! (curr l== nil));
	int ret := curr.key;
}

bb g-list-nth-data-after-loop-nil:
pre: ((dll^(list) & ((lseg^(list, curr) & ((lseg-length^(list, curr) + order) i= n)) * dll^(curr))) & (0 <= order));
post: (dll^(list) & (((length^(list) <= n) & (ret i= 0)) | ((n < length^(list)) & (((lseg^(list, curr) & (lseg-length^(list, curr) i= n)) * dll^(curr)) & ((curr l= nil) | ((curr |-> int key: ret) * true))))));
{
	assume ((! (0 < order)) || (curr l== nil));
	assume (curr l== nil);
	int ret := 0;
}
